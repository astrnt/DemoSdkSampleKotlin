apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.ben-manes.versions'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    dexOptions {
        maxProcessCount 4
        preDexLibraries false
        javaMaxHeapSize "8g"
    }

    defaultConfig {
        applicationId "co.astrnt.kyck"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        // Major -> Millions, Minor -> Thousands, Bugfix -> Hundreds. E.g 1.3.72 == 1,003,072
        versionName '0.0.1'

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    flavorDimensions "mode"
    productFlavors {
        beta {
            dimension "mode"
            applicationIdSuffix ".beta"
            buildConfigField "boolean", "BETA", "true"
            buildConfigField("String", "API_URL", '"https://beta.astrnt.co/api/"')
            buildConfigField("int", "SDK_VERSION", "1")
            resValue "string", "app_name", "BETA KYCK App"
        }
        live {
            dimension "mode"
            buildConfigField "boolean", "BETA", "false"
            buildConfigField("String", "API_URL", '"https://app.astrnt.co/api/"')
            buildConfigField("int", "SDK_VERSION", "1")
            resValue "string", "app_name", "KYCK App"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        textOutput "stdout"
        textReport true
        checkAllWarnings true
        warningsAsErrors true
        showAll true
        explainIssues true
        abortOnError false
        lintConfig file("$projectDir/lint.xml")
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'LICENSE.txt'
        exclude 'META-INF/license/LICENSE.base64.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/MANIFEST.MF'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    androidExtensions {
        experimental = true
    }
}

configurations.all {
    resolutionStrategy {
        force 'com.squareup.okio:okio:1.16.0'
        force "com.squareup.okhttp3:okhttp:3.12.0"
    }
}

apply from: 'dependencies.gradle'

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.1.0"

    implementation 'com.github.astrnt:demo-sdk:0.0.6'

    implementation 'com.otaliastudios:transcoder:0.7.0'

    implementation 'com.github.derohimat:sweet-alert-dialog:1.5.5'

    implementation("com.github.esafirm.android-image-picker:imagepicker:1.13.1", {
        exclude group: 'com.github.bumptech.glide', module: 'glide'
    })

    implementation supportLibs
    implementation googlePlayLibs
    implementation networkLibs
    implementation rxJavaLibs
    implementation otherLibs

    debugImplementation debugLibs
    releaseImplementation releaseLibs

    implementation(glideOkhttp) {
        exclude group: 'glide-parent'
    }

    debugImplementation(sherlock) {
        transitive = true
    }

    def uploadServiceVersion = "3.4.2"
    implementation "net.gotev:uploadservice:$uploadServiceVersion"
    implementation "net.gotev:uploadservice-okhttp:$uploadServiceVersion"

    // APT dependencies
    kapt annotationProcessorLibs
}